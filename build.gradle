/*
 * Copyright (c) 2019-Present Pivotal Software Inc, All Rights Reserved.
 *
 * Licensed under the Apache License, Version 2.0 (the "License");
 * you may not use this file except in compliance with the License.
 * You may obtain a copy of the License at
 *
 *       https://www.apache.org/licenses/LICENSE-2.0
 *
 * Unless required by applicable law or agreed to in writing, software
 * distributed under the License is distributed on an "AS IS" BASIS,
 * WITHOUT WARRANTIES OR CONDITIONS OF ANY KIND, either express or implied.
 * See the License for the specific language governing permissions and
 * limitations under the License.
 */

buildscript {
  repositories {
	maven { url "https://repo.spring.io/plugins-release" }
  }
  dependencies {
	classpath 'org.springframework.build.gradle:propdeps-plugin:0.0.7'
  }
}

plugins {
  id "org.jetbrains.kotlin.jvm" version "1.2.51"
  id "org.jetbrains.dokka" version "0.9.16"
}

apply from: "gradle/publishing.gradle"

configure(rootProject)  {
  apply plugin: 'kotlin'
  apply plugin: 'propdeps'

  description = 'Kotlin extensions for Project Reactor'
  group = 'io.projectreactor.kotlin'

  repositories {
	if (version.endsWith('BUILD-SNAPSHOT') || project.hasProperty('platformVersion')) {
	  maven { url 'https://repo.spring.io/libs-snapshot' }
	}

	jcenter()
	maven { url 'https://repo.spring.io/libs-milestone' }
  }

  configurations.all {
	resolutionStrategy {
	  eachDependency {
		switch (requested.module.toString()) {
		  case "io.projectreactor:reactor-core":
		  case "io.projectreactor:reactor-adapter":
		  case "io.projectreactor:reactor-extra":
		  case "io.projectreactor:reactor-test":
			return useVersion("3.3.0.BUILD-SNAPSHOT")
		}
	  }
	}
  }

  dependencies {
	testCompile 'junit:junit:4.12'
	testCompile 'org.assertj:assertj-core:3.12.2'
  }

  tasks.withType(org.jetbrains.kotlin.gradle.tasks.KotlinCompile).all {
	kotlinOptions.jvmTarget = "1.8"
	kotlinOptions.freeCompilerArgs = ["-Xjsr305=strict"]
  }
}

task kdocZip(type: Zip, dependsOn: dokka) {
  //ends up similar to javadoc jar: reactor-core-xxxx.RELEASE-kdoc.zip
  classifier = 'kdoc'
  from(dokka.outputDirectory)
}

artifacts {
  archives kdocZip
}

dependencies {
  compile 'io.projectreactor:reactor-core'
  optional 'io.projectreactor:reactor-test'
  optional 'io.projectreactor:reactor-extra'
  optional 'io.projectreactor:reactor-adapter'
  
  optional "io.reactivex.rxjava2:rxjava:2.2.8"

  optional 'org.jetbrains.kotlin:kotlin-stdlib'

  testCompile 'io.projectreactor:reactor-test'
}